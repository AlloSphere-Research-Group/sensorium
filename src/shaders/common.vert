#version 330
uniform mat4 al_ModelViewMatrix;
uniform mat4 al_ProjectionMatrix;

layout(location = 0) in vec3 position;
layout(location = 2) in vec2 texcoord;

uniform float eye_sep;
uniform float foc_len;

out vec2 var_texcoord;

vec4 stereo_displace(vec4 v, float e, float f) {
  // eye to vertex distance
  float l = sqrt((v.x - e) * (v.x - e) + v.y * v.y + v.z * v.z);
  // absolute z-direction distance
  float z = abs(v.z);
  // x coord of projection of vertex on focal plane when looked from eye
  float t = f * (v.x - e) / z;
  // x coord of displaced vertex to make displaced vertex be projected on focal
  // plane when looked from origin at the same point original vertex would be
  // projected when looked form eye
  v.x = z * (e + t) / f;
  // set distance from origin to displaced vertex same as eye to original vertex
  v.xyz = normalize(v.xyz);
  v.xyz *= l;
  return v;
}

void main() {
  gl_Position = al_ProjectionMatrix *
                stereo_displace(al_ModelViewMatrix * vec4(position, 1.0),
                                eye_sep, foc_len);

  var_texcoord = texcoord;
}